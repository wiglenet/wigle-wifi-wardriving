import groovy.sql.Sql

apply plugin: 'com.android.application'
apply plugin: 'de.undercouch.download'
apply plugin: 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'

android {
    namespace "net.wigle.wigleandroid"
    compileSdk = 36
    defaultConfig {
        applicationId "net.wigle.wigleandroid"
        minSdkVersion 24
        targetSdkVersion 36
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/ASL2.0', 'META-INF/LICENSE', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/LICENSE.txt', 'META-INF/MANIFEST.MF']
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled = true
    }
}

configurations {
    driver
}

dependencies {
    driver 'org.xerial:sqlite-jdbc:3.42.0.0'
}

// make a driver copy, per https://stackoverflow.com/questions/51676049/cannot-change-strategy-of-configuration-sqlite
// NOTE: this continues to result in warnings: Configuration 'driver' was resolved during configuration time.\n This is a build performance and scalability issue.
URLClassLoader loader = GroovyObject.class.classLoader
def driverCopy = configurations.driver.copy()
driverCopy.each {File file -> loader.addURL(file.toURI().toURL())}

ext {
    inputMxcSqlDir = layout.projectDirectory.dir("etc/mccmncsqlite")
    outputMxcDbFile = layout.projectDirectory.file("src/main/assets/mmcmnc.sqlite")
    mxcDictionaryFile = inputMxcSqlDir.file("mcc-mnc-dict.json")
    outputBtMemberUuidFile = layout.projectDirectory.file("src/main/assets/btmember.yaml")
    outputBtCoIdFile = layout.projectDirectory.file("src/main/assets/btco.yaml")
    outputBtServiceUuidFile = layout.projectDirectory.file("src/main/assets/ble_svc_uuids.yaml")
    outputBtCharacteristicUuidFile = layout.projectDirectory.file("src/main/assets/ble_char_uuids.yaml")
    outputBtCharacteristicAppearanceFile = layout.projectDirectory.file("src/main/assets/appearance_values.yaml")
}

def camerax_version = "1.5.2"

dependencies {
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.legacy:legacy-support-core-utils:1.0.0"
    implementation "androidx.core:core:1.17.0"
    implementation "androidx.appcompat:appcompat:1.7.1"
    //NB: 1.12 -  1.14.0-alpha08 are unstable - see byzantine failures in https://stackoverflow.com/questions/79128940/mysterious-accessibility-related-crash
    implementation "com.google.android.material:material:1.11.0"
    implementation "com.google.android.gms:play-services-maps:19.2.0"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:$camerax_version"
    implementation 'com.google.android.gms:play-services-mlkit-text-recognition:19.0.1'
    implementation 'com.google.android.gms:play-services-mlkit-barcode-scanning:18.3.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.slf4j:slf4j-android:1.7.19'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.11.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.11.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.1'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation (name:'MaskedEditText-1.0.5', ext:'aar')
    implementation 'com.goebl:simplify:1.0.0'
    implementation "com.appmattus.certificatetransparency:certificatetransparency-android:2.8.20"
    implementation 'com.google.maps.android:android-maps-utils:3.1.1'
    implementation 'org.apache.commons:commons-csv:1.10.0'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.1'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.github.razir.progressbutton:progressbutton:2.1.0'
    implementation 'org.maplibre.gl:android-sdk:12.3.1'
    implementation 'org.maplibre.gl:android-plugin-annotation-v9:3.0.2'
    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.7.0'
    testImplementation 'org.mockito:mockito-inline:4.11.0'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}

import groovy.json.JsonSlurper
import java.sql.SQLException

//TODO: this runs every time, which isn't optimal. just needs to run before build
tasks.register('downloadFile') {
    println "===MCC/MNC DB Creation==="
    download.run {
        src 'https://api.wigle.net/api/v2/cell/mccMnc'
        dest mxcDictionaryFile
        overwrite true
    }
    File test = new File(mxcDictionaryFile.toString())
    if (test.exists()) {
        println "Wrote $mxcDictionaryFile"
    } else {
        throw new IOException("Failed to download mcc-mnc file: $mxcDictionaryFile")
    }

}

tasks.register('createMccMncDB') {
    dependsOn 'downloadFile'
    ext.loadScript = { sql, file ->
        println("Applying " + file)
        String[] sqlcmds = file.text.split(";")
        sqlcmds.collect { it.trim() }.findAll { !it.isEmpty() && !it.startsWith("--") }.each {
            try {
                sql.execute(it)
            } catch (SQLException e) {
                System.err << "Invalid SQL statement: " + it
                e.printStackTrace(System.err)
                throw new GradleException('Preformulated MCC/MNC Database SQL Failure.')
            }
        }
    }

    delete outputMxcDbFile
    def sql = Sql.newInstance("jdbc:sqlite:$outputMxcDbFile", '', '', 'org.sqlite.JDBC')
    new File("$inputMxcSqlDir/").eachFileMatch(~/.*\.sql/) { loadScript(sql, it) }
    def inputFile = new File(mxcDictionaryFile.toString())
    def mccMap = new JsonSlurper().parseText(inputFile.text)
    assert mccMap instanceof Map
    def insertSql = 'INSERT OR REPLACE INTO wigle_mcc_mnc (mcc, mnc, type, countryName, countryCode, brand, operator, status, bands, notes) VALUES (?,?,?,?,?,?,?,?,?,?)'
    def myKeyNames = ['mcc', 'mnc']
    mccMap.each { mcc, mncMap ->
        mncMap.each { mnc, value ->
            def type = value?.type
            def countryName = value?.countryName
            def countryCode = value?.countryCode
            def brand = value?.brand
            def operator = value?.operator
            def status = value?.status
            def bands = value?.bands
            def notes = value?.notes
            def params = [mcc, mnc, type, countryName, countryCode, brand, operator, status, bands, notes]
            sql.executeInsert insertSql, params, myKeyNames
        }
    }
    println "MCC/MNC Database created: $outputMxcDbFile"
    sql.close()
}

ext.downloadIfStale = { localFile, remoteFile, title ->
    def MAX_DL_SKEW = 864000000 // 10 days in ms
    File currentFile = new File(localFile.toString())
    def modifiedMillisecondsAgo = new Date().time - currentFile.lastModified()
    def secondsMod = modifiedMillisecondsAgo/1000
    println("$title: modified $secondsMod s ago / exists " + currentFile.exists())
    if (!currentFile.exists() || (modifiedMillisecondsAgo > MAX_DL_SKEW)) {
        println "===$title==="
        download.run {
            src remoteFile //'https://api.bitbucket.org/2.0/repositories/bluetooth-SIG/public/src/main/assigned_numbers/uuids/member_uuids.yaml'
            dest localFile.toString()
            overwrite true
        }
        File testFile = new File(localFile.toString())
        if (testFile.exists()) {
            println "Wrote $localFile"
        } else {
            throw new IOException("Failed to download $title: $localFile")
        }
    }
}

//TODO: this runs every time, which isn't optimal. just needs to run before build
tasks.register('downloadBtMemberFile') {
    downloadIfStale(outputBtMemberUuidFile, 'https://api.bitbucket.org/2.0/repositories/bluetooth-SIG/public/src/main/assigned_numbers/uuids/member_uuids.yaml', 'BLE Member UUID YAML')
    downloadIfStale(outputBtCoIdFile, 'https://api.bitbucket.org/2.0/repositories/bluetooth-SIG/public/src/main/assigned_numbers/company_identifiers/company_identifiers.yaml', 'BLE Company ID YAML')
    downloadIfStale(outputBtServiceUuidFile, 'https://api.bitbucket.org/2.0/repositories/bluetooth-SIG/public/src/main/assigned_numbers/uuids/service_uuids.yaml', 'BLE Service UUID YAML')
    downloadIfStale(outputBtCharacteristicUuidFile, 'https://api.bitbucket.org/2.0/repositories/bluetooth-SIG/public/src/main/assigned_numbers/uuids/characteristic_uuids.yaml', 'BLE Characteristic UUID YAML')
    downloadIfStale(outputBtCharacteristicAppearanceFile, 'https://api.bitbucket.org/2.0/repositories/bluetooth-SIG/public/src/main/assigned_numbers/core/appearance_values.yaml', 'BLE Appearance YAML')
}


build.dependsOn downloadFile, createMccMncDB, downloadBtMemberFile
