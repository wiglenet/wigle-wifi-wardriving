apply plugin: 'com.android.application'
apply plugin: 'de.undercouch.download'
apply plugin: 'groovyx.grooid.groovy-android'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "net.wigle.wigleandroid"
        minSdkVersion 14
        targetSdkVersion 27
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }
}

configurations {
    driver
}

dependencies {
    driver 'org.xerial:sqlite-jdbc:3.23.1'
}

URLClassLoader loader = GroovyObject.class.classLoader
configurations.driver.each {File file -> loader.addURL(file.toURI().toURL())}

ext {
    supportLibraryVersion = '27.1.1'
    gmsLibraryBaseVersion = '15.0.1'
    inputMxcSqlDir = "etc/mccmncsqlite"
    outputMxcDbFile = "$projectDir/src/main/assets/mmcmnc.sqlite"
    mxcDictionaryFile = "$inputMxcSqlDir/mcc-mnc-dict.json"
}

dependencies {
    implementation project(':android-maps-utils')
    implementation "com.android.support:support-compat:$supportLibraryVersion"
    implementation "com.android.support:support-core-utils:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.7'
    implementation "com.google.android.gms:play-services-maps:$gmsLibraryBaseVersion"
    implementation 'com.google.android.gms:play-services-vision:15.0.2'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'org.slf4j:slf4j-android:1.7.19'
    implementation files('libs/TTS_library_stub.jar')
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.0.pr3'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.0.pr3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.0.pr3'
    implementation 'ru.egslava:MaskedEditText:1.0.5'
}

import groovy.json.JsonSlurper

//TODO: this runs every time, which isn't optimal. just needs to run before build
task downloadFile (type: Download) {
    println "downloading mcc/mnc data"
    download {
        src 'https://api.wigle.net/api/v2/cell/mccMnc'
        dest "../$mxcDictionaryFile"
        overwrite true
        acceptAnyCertificate true /* horrible, but alternative is update trust store <=> letsencrypt */
    }
}

task createMccMncDB (dependsOn: 'downloadFile') {
    ext.loadScript = { sql, file ->
        println("Applying " + file)
        String[] sqlcmds = file.text.split(";")
        sqlcmds.collect{it.trim()}.findAll{!it.isEmpty() && !it.startsWith("--")}.each{
            try {
                sql.execute(it)
            } catch(java.sql.SQLException e) {
                System.err << "Invalid SQL statement: " + it
                e.printStackTrace(System.err)
                throw new GradleException('Preformulated MCC/MNC Database SQL Failure.')
            }
        }
    }

    delete outputMxcDbFile
    def sql = groovy.sql.Sql.newInstance("jdbc:sqlite:$outputMxcDbFile",'','','org.sqlite.JDBC')
    new File("$inputMxcSqlDir/").eachFileMatch(~/.*\.sql/) {loadScript(sql, it)}
    def inputFile = new File("$mxcDictionaryFile")
    def mccMap = new JsonSlurper().parseText(inputFile.text)
    def insertSql = 'INSERT OR REPLACE INTO wigle_mcc_mnc (mcc, mnc, type, countryName, countryCode, brand, operator, status, bands, notes) VALUES (?,?,?,?,?,?,?,?,?,?)'
    def myKeyNames = ['mcc', 'mnc']
    mccMap.each { mcc, mncMap ->
        //DEBUG: println "mcc: $mcc"
        mncMap.each {mnc, value ->
            //DEBUG: println "\tmnc: $mnc"
            //DEBUG: println "\t\tvalue: $value"
            def type = value?.type
            def countryName = value?.countryName
            def countryCode = value?.countryCode
            def brand = value?.brand
            def operator = value?.operator
            def status = value?.status
            def bands = value?.bands
            def notes = value?.notes
            def params = [mcc, mnc, type, countryName, countryCode, brand, operator, status, bands, notes]
            sql.executeInsert insertSql, params, myKeyNames
        }
    }
    sql.close()
}
